#version 330 core

// Input data
in vec2 UV;

// Output date
out vec4 color;

// Main terrain texture
uniform sampler2D textureSamplerWorld;

// Gaea terrain masks
uniform sampler2D textureSamplerSlope;
uniform sampler2D textureSamplerSoil;
uniform sampler2D textureSamplerPeaks;
uniform sampler2D textureSamplerLake;
uniform sampler2D textureSamplerRivers;

// High-resolution material textures
uniform sampler2D textureSamplerRock;
uniform sampler2D textureSamplerGrass;
uniform sampler2D textureSamplerDirt;
uniform sampler2D textureSamplerSand;

// Moving water texture + displacement
uniform float time;
uniform sampler2D textureSamplerWater;
uniform sampler2D displacementTextureSampler;

// Cheap ALU-only hash functions for GLSL
float hash(vec2 p)
{
    p = fract(p * 0.3183099 + vec2(0.71, 0.113));
    return fract(37.0 * p.x * p.y);
}

vec2 rotate(vec2 uv, float r)
{
    int k = int(floor(r * 4.0));

    if (k == 0)      return uv;
    else if (k == 1) return vec2(-uv.y,  uv.x);
    else if (k == 2) return vec2(-uv.x, -uv.y);
    else             return vec2( uv.y, -uv.x);
}

// ======================< Main function >====================== //
void main()
{
    // Gaea terrain masks
    float slope = texture(textureSamplerSlope, UV).r;
    float soil  = texture(textureSamplerSoil,  UV).r;

    float lake = texture(textureSamplerLake,  UV).r;
    lake       = smoothstep(0.2, 0.8, lake);



    vec3 worldColor = texture(textureSamplerWorld, UV).rgb;
    vec3 finalColor; // Final output color
    vec3 rock;

    // Tile scale
    float tileScale = 50.0f;
    vec2  tileLocal = fract(UV * tileScale); // 0..1 inside tile

    { // ROCK, GRASS, DIRT... (Land)

        vec2  tile       = floor(UV * tileScale);
        float tileRandom = hash(tile);

        tileLocal = tileLocal - 0.5; // Center Pivot
        tileLocal = rotate(tileLocal, tileRandom);
        tileLocal = tileLocal + 0.5;

        // Sample materials using tile-local UVs!
        rock       = texture(textureSamplerRock,  tileLocal).rgb;
        vec3 grass = texture(textureSamplerGrass, tileLocal).rgb;
        vec3 dirt  = texture(textureSamplerDirt,  tileLocal).rgb;

        // Blending logic
        vec3 base1 = mix(dirt, grass, soil);
        vec3 base2 = mix(rock, base1, slope * 1.2);

        // Blend with Gaea terrain texture
        finalColor = mix(base2, worldColor, 0.3);

    }

    { // WATERRR... (Lakes)

        float waterTiling = 20.0; // How many times the water texture repeats across the terrain
        vec2 baseUV       = UV * waterTiling;

        // Scroll speed for the water
        vec2 waterScroll = vec2(time * 0.03, time * 0.02);
        vec2 waterUV     = baseUV + waterScroll;

        // Wave pattern depending on position + time
        float waveSpeed     = 1.0;
        float waveFrequency = 15.0;
        float waveStrength  = 0.02;

        float wave = sin(baseUV.x * waveFrequency + time * waveSpeed) *
                     cos(baseUV.y * waveFrequency + time * waveSpeed);

        // Distort UV slightly by the wave
        waterUV += vec2(wave, wave) * waveStrength;

        vec3 movingWater = texture(textureSamplerWater, waterUV).rgb;

        // Displacement texture for more detail (WATER)
        vec3 displacementTexture = texture(displacementTextureSampler, UV + 0.9*sin(time)).rgb;
        vec3 surfaceTexture      = mix(movingWater, displacementTexture, 0.2);
        vec3 displacedWater      = texture(textureSamplerWater, waterUV + length(surfaceTexture)*0.04).rgb;

        // ... AND SAND!
        vec3 sand      = texture(textureSamplerSand, tileLocal).rgb;
        vec3 sandWater = mix(sand, displacedWater, 0.6).rgb;

        // Blend only where lake mask is > 0
        finalColor = mix(finalColor, sandWater, lake);

    }

    { // RIVERS...

        float riverMask = texture(textureSamplerRivers, UV).r;
        riverMask       = smoothstep(0.05, 0.4, riverMask);

        // HEIGHTMAP SAMPLING
        float h   = texture(textureSamplerPeaks, UV).r;
        float hx1 = texture(textureSamplerPeaks, UV + vec2(0.01, 0.0)).r;
        float hx2 = texture(textureSamplerPeaks, UV - vec2(0.01, 0.0)).r;
        float hy1 = texture(textureSamplerPeaks, UV + vec2(0.0,  0.01)).r;
        float hy2 = texture(textureSamplerPeaks, UV - vec2(0.0,  0.01)).r;

        // Central difference
        vec2 grad = -vec2(hx1 - hx2, hy1 - hy2);
        if (length(grad) < 0.0001)
            grad = vec2(0.7, -0.4);

        vec2 flowDir = normalize(grad);

        // River animation
        float riverTiling = 12.0;
        float riverSpeed  = 1;

        vec2 riverUV = UV * riverTiling + flowDir * time * riverSpeed;

        // Add gentle noise drift
        float n = texture(displacementTextureSampler, UV * 4.0 + time * 0.1).r;
        riverUV += flowDir * (n * 0.02);

        // Ripple effect
        float wobble = sin(time * 0.5 + UV.y * 20.0) * 0.003;
        riverUV     += flowDir * wobble;

        vec3 riverWater = texture(textureSamplerWater, riverUV).rgb;
        vec3 rockyRiver = mix(rock, riverWater, 0.65);

        // Rivers should NOT overwrite lakes!
        float riverBlend = riverMask * (1.0 - lake);

        finalColor = mix(finalColor, rockyRiver, riverBlend);

    }

    // Output
    color = vec4(finalColor, 1.0);
}
