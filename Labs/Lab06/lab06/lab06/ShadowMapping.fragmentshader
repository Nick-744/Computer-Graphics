#version 330 core

in vec4 vertex_position_cameraspace;
in vec4 vertex_normal_cameraspace;
in vec4 light_position_cameraspace1;
in vec2 vertex_UV;
in vec4 vertex_position_lightspace1;

// ===< HOMEWORK 2 >=== //
in vec4 light_position_cameraspace2;
in vec4 vertex_position_lightspace2;



uniform sampler2D shadowMapSampler1;
uniform sampler2D diffuseColorSampler;
uniform sampler2D specularColorSampler;

uniform sampler2D shadowMapSampler2; // HOMEWORK 2

uniform int useTexture = 0;

// light properties
struct Light
{
    vec4 La;
    vec4 Ld;
    vec4 Ls;
    vec3 lightPosition_worldspace;
};
uniform Light light1;

uniform Light light2; // HOMEWORK 2

// materials
struct Material
{
    vec4 Ka; 
    vec4 Kd;
    vec4 Ks;
    float Ns; 
};
uniform Material mtl;

out vec4 fragmentColor;

uniform int ChampionOfLight; // HOMEWORK 4



vec4 phong(Light light, float visibility, vec4 light_position_cameraspace);
float ShadowCalculation(vec4 vertex_position_lightspace, sampler2D shadowMapSampler);

void main()
{
    fragmentColor = vec4(0.0);

    // Task 4.3
    float shadow = ShadowCalculation(vertex_position_lightspace1, shadowMapSampler1);
    float visibility = 1.0f - shadow;

    fragmentColor += 0.5 * phong(light1, visibility, light_position_cameraspace1);

    // ===< HOMEWORK 2 >=== //
    float shadow2 = ShadowCalculation(vertex_position_lightspace2, shadowMapSampler2);
    float visibility2 = 1.0f - shadow2;

    fragmentColor += 0.5 * phong(light2, visibility2, light_position_cameraspace2);
}



float ShadowCalculation(vec4 vertex_position_lightspace, sampler2D shadowMapSampler)
{
    // Given the position of a fragment in lightspace coordinates
    // We sample the depth buffer to see whether or not the fragment is shadowed
    
    float shadow;

    // Task 4.3
    // Perspective devide to bring coordinates in range[-1, 1]
    vec3 projCoords = vertex_position_lightspace.xyz / vertex_position_lightspace.w;

    // Since the depth map values are in range[0, 1]
    projCoords = 0.5f * projCoords + 0.5f;

    // Sampling the closest point in this position from the depth map
    // REMINDER: Since we are in lightspace coordinates,
    //           the z parameter is the depth from the camera
    float closestDepth = texture(shadowMapSampler, projCoords.xy).r;

    // Then we get the depth of the current fragment
    float currentDepth = projCoords.z;

    // If the current fragment is farther than the closest point, the fragment is shadowed
    shadow = (currentDepth > closestDepth) ? 1.0f : 0.0f;

    // Task 4.4
    // Correcting the quantization problem
    float bias = 0.005;
    shadow = (currentDepth > closestDepth + bias) ? 1.0f : 0.0f;

    // Task 4.6 Make the shadow edges more realistic
    shadow = 0.0;
    vec2 depthMap_dimensions = textureSize(shadowMapSampler, 0);
    vec2 texelSize = 1.0 / depthMap_dimensions;
    for(int x = -1; x <= 1; x++ )
        for(int y = -1; y <= 1; y++ )
        {
            float pcfDepth = texture(shadowMapSampler, projCoords.xy + vec2(x, y) * texelSize).r;
            shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;
        }
    shadow /= 9.0;

    // Task 4.5
    if(projCoords.z > 1) shadow = 0.0;

    return shadow;
}



vec4 phong(Light light, float visibility, vec4 light_position_cameraspace)
{
    vec4 _Ks = mtl.Ks;
    vec4 _Kd = mtl.Kd;
    vec4 _Ka = mtl.Ka;
    float _Ns = mtl.Ns;

    // use texture for materials
    if (useTexture == 1)
    {
        _Ks = vec4(texture(specularColorSampler, vertex_UV).rgb, 1.0);
        _Kd = vec4(texture(diffuseColorSampler, vertex_UV).rgb, 1.0);
        _Ka = vec4(0.05 * _Kd.rgb, _Kd.a);
        _Ns = 10;
    }
    
    // model ambient intensity (Ia)
    vec4 Ia = light.La * _Ka;

    if (ChampionOfLight == 1) return vec4(Ia * 10); // HOMEWORK 4

    // model diffuse intensity (Id)
    vec4 N = normalize(vertex_normal_cameraspace);
    vec4 L = normalize(light_position_cameraspace - vertex_position_cameraspace);
    float cosTheta = clamp(dot(N, L), 0, 1);
    vec4 Id = light.Ld * _Kd * cosTheta;

    // model specular intensity (Is)
    vec4 R = reflect(-L, N);
    vec4 E = normalize(vec4(0, 0, 0, 1) - vertex_position_cameraspace);
    float cosAlpha = clamp(dot(E, R), 0, 1);
    float specular_factor = pow(cosAlpha, _Ns);
    vec4 Is = light.Ls * _Ks * specular_factor;

    // here we would normally model the light attenuation effect
    // but since this is a directional light source that is infinitely far away
    // attenuating the light intensity does not make sense

    // final fragment color
    return vec4(
        Ia + 
        Id * visibility + // Task 4.3 Use visibility
        Is * visibility);
}
